<!-- AMXX Browser V0.2 alpha -->
<!-- 2020-05-13 By Schmurtz -->
<html>
    <head>
		<meta charset="utf-8">
		<meta http-equiv="x-ua-compatible" content="IE=10">
		<meta http-equiv="content-type" content="text/html">
		<meta name="viewport" content="width=device-width, initial-scale=1">  
		
		
		<title>Amxx Browser</title>
		<HTA:APPLICATION
		  ID = "Amxx Browser"
		  icon="http://files.softicons.com/download/social-media-icons/free-large-twitter-icons-by-aha-soft/ico/Audio.ico"
		  APPLICATIONNAME="Amxx Browser"
		  SCROLL="no"
		  navigable="no"
		  selection="no"
		  showintaskbar="yes"
		  singleinstance="no"
		  innerborder="no"
		  maximizebutton="no"
		  minimizebutton="yes"
		  border="dialog"
		  borderstyle="normal"
		  caption="yes"
		  contextMenu="no"
		  sysmenu="yes"
		 >
		
		<!--  ========================================== jquery ========================================== -->
			<!--  <script src='https://bootswatch.com/_vendor/jquery/dist/jquery.min.js'></script>    v3.4.1-->
			<!--  <script src='https://ajax.googleapis.com/ajax/libs/jquery/1.11.3/jquery.min.js'> </script>  -->
			<!--  <script src='https://ajax.googleapis.com/ajax/libs/jquery/3.1.1/jquery.min.js'>  </script>  -->
			<!--  <script src="https://ajax.googleapis.com/ajax/libs/jquery/3.2.1/jquery.min.js"></script>-->
			<!-- <script src="https://code.jquery.com/jquery-3.5.1.js"></script> -->
			<script src="https://ajax.googleapis.com/ajax/libs/jquery/3.5.1/jquery.min.js">  </script>

		<!--  ======================================= bootstrapcdn ======================================= -->
			
			<!--  <link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootswatch/4.4.1/darkly/bootstrap.min.css">-->
			<link rel='stylesheet' href='https://bootswatch.com/3/darkly/bootstrap.min.css'>
			<script src='https://maxcdn.bootstrapcdn.com/bootstrap/3.4.1/js/bootstrap.min.js'></script>


		<!--  ======================================== Table Filter ======================================== -->

			<!-- jQuery.FilterTable : more powerful filter -->
			<!--  <script type="text/javascript" src='dependencies\jQuery.FilterTable\jquery.filtertable.js'></script>   -->

			<!-- Even  more powerful filter ! -->
			
			<link rel="stylesheet" type="text/css" href="https://cdn.datatables.net/v/bs/dt-1.10.21/b-1.6.2/b-colvis-1.6.2/b-html5-1.6.2/b-print-1.6.2/kt-2.5.2/sc-2.0.2/sp-1.1.0/datatables.min.css"/>
			<script type="text/javascript" src="https://cdn.datatables.net/v/bs/dt-1.10.21/b-1.6.2/b-colvis-1.6.2/b-html5-1.6.2/b-print-1.6.2/kt-2.5.2/sc-2.0.2/sp-1.1.0/datatables.min.js"></script>

			<!-- Working fine but not perfect CSS with bootstrapcdn
			<link rel="stylesheet" type="text/css" href="https://cdn.datatables.net/v/bs/jqc-1.12.4/datatables.min.css"/>
			<script type="text/javascript" src="https://cdn.datatables.net/1.10.21/js/jquery.dataTables.min.js"></script>
			-->

		<!--  ==================================== GIF animated library ==================================== -->

			<!--  https://raw.githubusercontent.com/krasimir/gifffer/master/build/gifffer.min.js   -->
			<script type="text/javascript" src="dependencies/gifffer.min.js"></script>

			
		<!--  ==================================== Video player library ==================================== -->
			<!-- video-js Support (better player than the default integrated in IE/hta-->
			<link href="https://vjs.zencdn.net/7.8.1/video-js.css" rel="stylesheet" />

			<!-- If you'd like to support IE8 (for Video.js versions prior to v7) -->
			<script src="https://vjs.zencdn.net/ie8/1.1.2/videojs-ie8.min.js"></script>
			
			
			
			
		<!--  *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*= CSS Management *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*= -->
					

		<!-- remove bugged IE clear cross in filter input -->
		<!-- 	<style>::-ms-clear { display: none; } </style>    -->
		
		<!--  *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*= -->
		 <!-- Color for the table cell when filter is active  (declaration for  jQuery.FilterTable)  -->
		<!-- 
		<style> 
		.filter-table .quick { margin-left: 0.5em; font-size: 0.8em; text-decoration: none; }
		.fitler-table .quick:hover { text-decoration: underline; }
		td.alt { background-color: #ffc; background-color: rgba(0, 255, 0, 0.2); }
		</style>
		-->
		
		<!--  *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*= -->
		
		<!--     If you want to keep table header on top, uncomment this CSS section-->
		<!-- 

		table {    width: 100%;}
		thead, tbody, tr, td, th { display: block; }
		tr:after { content: ' '; display: block;  visibility: hidden;  clear: both; }
		thead th { height: 30px; /*text-align: left;*/ }
		tbody { height: 520px; overflow-y: auto; }
		thead { /* fallback */}
		tbody td, thead th { width: 19.2%; float: left; }
		-->
		
		<!--  *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*= -->
		<style> 
		 tfoot input {
				width: 100%;
				padding: 3px;
				box-sizing: border-box;
			}
		</style>
			

    </head>



<body>
<h3>Amxx Plugin brower</h3>


<SCRIPT language="VBScript">
Dim CurPluginNumber
</SCRIPT>




<SCRIPT language="VBScript">
Sub Start_Button
document.GetElementById("chk_2").checked = true
'msgbox "chk_"	
'msgbox CurPluginNumber
    For i = 1 To CurPluginNumber
        If document.GetElementById("chk_"& i).checked Then
			msgbox "chk_"& i
        End If
    Next
End Sub
</SCRIPT>
	

<!--   ----------------------------------  Tabs ----------------------------------    -->

<ul class="nav nav-tabs">
  <li class="active"><a href="#home" data-toggle="tab">Home</a></li>
  <li><a href="#profile" data-toggle="tab">Profile</a></li>
  <li class="disabled"><a>Disabled</a></li>
  <li class="dropdown">
    <a class="dropdown-toggle" data-toggle="dropdown" href="#">
      Dropdown <span class="caret"></span>
    </a>
    <ul class="dropdown-menu">
      <li><a href="#dropdown1" data-toggle="tab">Action</a></li>
      <li class="divider"></li>
      <li><a href="#dropdown2" data-toggle="tab">Another action</a></li>
    </ul>
  </li>
</ul>
<!--   ---------------------------------------------------------------------------    -->


<div id="myTabContent" class="tab-content">
  <div class="tab-pane fade active in" id="home">
	<BR>
  <!--   --------------------------- some tests for later....
  <img src="data:image/gif;base64,R0lGODlhEAAOALMAAOazToeHh0tLS/7LZv/0jvb29t/f3//Ub//ge8WSLf/rhf/3kdbW1mxsbP//mf///yH5BAAAAAAALAAAAAAQAA4AAARe8L1Ekyky67QZ1hLnjM5UUde0ECwLJoExKcppV0aCcGCmTIHEIUEqjgaORCMxIC6e0CcguWw6aFjsVMkkIr7g77ZKPJjPZqIyd7sJAgVGoEGv2xsBxqNgYPj/gAwXEQA7"width="16" height="14" alt="embedded folder icon">
  
  <Br>&nbsp;&nbsp;&nbsp;<input type="IMAGE" <img alt="Test Button" src="data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAACAAAAAgCAIAAAD8GO2jAAAABGdBTUEAALGPC/xhBQAAAAlwSFlzAAAOcwAADnMBOWAVqQAAAAd0SU1FB+QFCwwuArLzaiEAAAAYdEVYdFNvZnR3YXJlAHBhaW50Lm5ldCA0LjAuM4zml1AAAAJpSURBVEhLrZaBceIwEEWvk9AB6cB0AB1AB7iDpAPowO4AOggd2B3gDrgOuOf98loosknm+KPRSCv5fa1kC/5Ug/YvVYBWVTBIov+pmNYbqP/XdL/f44bXb4vibVlQ0/6JxBwNqA2bEbOL1aanq5iH4qqzcmzGgGGvJUerEaKRjPmgOYNEbds6Wo3dvuwbi2K92R2O1Wq9u3bd5XKBuy8/f2fA1BidaXvXLFfr7e8M9FiMY9XEv74uI9fin4cjcdHRjw0cPeCW76uxawV0XZ+AhmfMJmPQdd3tdlMdJiqDAZQpNoqlJsujX/+UQSLmxaCkPh5r4cqPw+l8NougSQPPQPWZjXaiFRZLkOdFEQ69Fxv6oWPKGyTinXO0bPoTfgRJbubeTwyURIxWaa+88Z1Y83pigJg0ood911D8FriSJCYNtHYp0OOyKHyCcFOaNIgV7rhh+fJYLFfyCKRIfgDUGQPunKttsUvEsUQbld2lWHkD9+B5vk8nsurR5pnHkwxkg2IouKZphabe7koiSESXnzPKG0iy8eXLAPU5DZZcdpbDZBJzBqiqT87abEtAGFD3kcGYq4JgQA7yJOYMyIDfEAfZ6kfJVUNTGaA5AxTodvkkrxbyUXUD8lEZA37zEjoNLnoFxZJ8VFsXkCZtUf4MMGiaZqRb0YEnSfgorKldyhhA59z8YZy4hLV8JA8tufywW3bialISGQMy8I+La5m/DiA9A9mISFfT6tNZEaFj5Q18i2gYcPzuULxRfQbLglS+HwPKnAHCANHlHR0MgpIMkNZBzQ9c9hjEDAbI+y9RTAsGSNFXKUCr6h/AoMU7QRkg5AAAAABJRU5ErkJggg==" 
width="30px" height="30px" align="middle" value = "Test Button" onclick="Start_Button	()"/> <Br><Br>

       ---------------------------  -->

<!-- <table class="table table-striped table-hover" id="PluginsTable"> -->
<!-- <table class="table table-hover" id="PluginsTable">     declaration for  jQuery.FilterTable -->

<table class="table table-hover" id="PluginsTable"> 
 <thead>
  <tr class="warning">
    <th>#</th>
    <th>Name</th>
    <th>Description</th>
	<th>Preview</th>
	<th>State</th>
  </tr>
</thead> 

  


<tbody>

<!--       <tr class="warning" id="filterRow">
        
        <th></th>
        <th><input type="text" id="NameInputFilter" onkeyup="FilterTable()" placeholder="Search for names.." title="Type in a name"></th>
        <th></th>
        <th></th>
		<th><input type="checkbox">Filter enabled</th>
      </tr> -->

<script type="text/vbs">

Set FSO = CreateObject("Scripting.FileSystemObject")
if not FSO.FolderExists(".\Plugins") then FSO.CreateFolder(".\Plugins") ' We check if the folder exist...
ShowSubfolders FSO.GetFolder(".\Plugins"), 1     'here we can indicate the depth of subfolders we want

Sub ShowSubFolders(Folder, Depth)
	
	Dim StrData
    If Depth > 0 then
		
        For Each Subfolder in Folder.SubFolders
            'Wscript.Echo Subfolder.Path
			StrData = ""
			CurPluginNumber = CurPluginNumber + 1
			document.write "<tr class=""table-secondary""><td>" & CurPluginNumber & "</td><td>" & Subfolder.name & "</td>"   ' <<<<<<<<<------------------  Here we write the number and the name of the plugin (based on folder name)
			
			' ============================  Description  ============================
			filepath = FSO.BuildPath(Subfolder.Path, "Description.txt")
			If FSO.FileExists(filepath) Then    ' Description file is the minimum to show a subfolder in the GUI
				Set File = fso.OpenTextFile(filepath)
				Do until File.atendofstream
					StrData = StrData & "<Br>" & trim(File.ReadLine)
				Loop
				File.close
				document.write "<td>" & StrData & "</td>"       ' <<<<<<<<<------------------  Here we write the description
			Else
				document.write "<td> No Description</td>"
				'Wscript.Echo "File does not exist."    ' Here we log that a subfolder exist without description
			End If
			' ============================  Preview File  ============================

			
			filepath = FSO.BuildPath(Subfolder.Path, "Preview.jpg")
			If FSO.FileExists(filepath) Then
				document.write "<td><img height='200' width='250' src=""" & filepath & """><div class='invisible'>jpg</div></td>"
			Else
				filepath = FSO.BuildPath(Subfolder.Path, "Preview.png")
				If FSO.FileExists(filepath) Then
					document.write "<td><img height='200' width='250' src=""" & filepath & """><div class='invisible'>png</div></td>"
				Else
					filepath = FSO.BuildPath(Subfolder.Path, "Preview.gif")
					If FSO.FileExists(filepath) Then
						document.write "<td><img data-gifffer-height='200' data-gifffer-width='250' data-gifffer=""" & filepath & """><div class='invisible'>gif</div></td>"
					Else
						filepath = FSO.BuildPath(Subfolder.Path, "Preview.mp4")
						If FSO.FileExists(filepath) Then
							'<!-- video-js Support (better player than the default integrated in IE/hta-->
							document.write "<td><video id='my-video' class='video-js'  controls  preload='auto'  width='250'  height='200'  data-setup='{}' ><source src=""" & filepath & """ type='video/mp4' />mp4</video></td>"  ' "mp4" is used for filter
							'<!-- Default hta player-->
							'document.write "<td><video width='250' height='200' controls>  <source src=""" & filepath & """ type='video/mp4'>Your browser does not support the video tag.</video></td>"
							'document.write "<td><embed src=""" & filepath & """ width=""250"" height=""200"" autostart=""false"" type=""video/mp4""></td>"
							'document.write "<td><iframe src='https://player.vimeo.com/video/298038460?loop=1&byline=0&portrait=0&title=0&transparent=0' allowtransparency></iframe></td>"
							'document.write "<td><iframe width='320' height='240	' src='https://www.youtube.com/embed/YGPm77kDddg' frameborder='0' allow='autoplay; encrypted-media; picture-in-picture' allowfullscreen></iframe></td>"
						Else
							document.write "<td> No Preview</td>"
							'Wscript.Echo "File does not exist."    ' Here we log that a subfolder exist without any Preview
						End If
					End If
				End If
			End If
			
			document.write "<td><input type=""checkbox"" id='chk_"& CurPluginNumber &"'><button type=""button"" class=""btn btn-success"">Add</button><button type=""button"" class=""btn btn-danger"">Remove</button></td></tr>"   ' <<<<<<<<<------------------  Here we write the actions column
            ShowSubFolders Subfolder, Depth -1    'recursive call
        Next
    End if

End Sub

</script>

<!--   This Tfoot will be transformed into search box thanks to search boxes -->
	
</tbody>
        <tfoot>
            <tr>
                <td></td>
                <th></th>
                <th></th>
                <th></th>
                <th></th>
            </tr>
        </tfoot>
</table>


<!--   Other tabs for futu use -->
</div>
  <div class="tab-pane fade" id="profile">
    <p>Food truck fixie locavore, accusamus mcsweeney's marfa nulla single-origin coffee squid. Exercitation +1 labore velit, blog sartorial PBR leggings next level wes anderson artisan four loko farm-to-table craft beer twee. Qui photo booth letterpress, commodo enim craft beer mlkshk aliquip jean shorts ullamco ad vinyl cillum PBR. Homo nostrud organic, assumenda labore aesthetic magna delectus mollit.</p>
  </div>
  <div class="tab-pane fade" id="dropdown1">
    <p>Etsy mixtape wayfarers, ethical wes anderson tofu before they sold out mcsweeney's organic lomo retro fanny pack lo-fi farm-to-table readymade. Messenger bag gentrify pitchfork tattooed craft beer, iphone skateboard locavore carles etsy salvia banksy hoodie helvetica. DIY synth PBR banksy irony. Leggings gentrify squid 8-bit cred pitchfork.</p>
  </div>
  <div class="tab-pane fade" id="dropdown2">
    <p>Trust fund seitan letterpress, keytar raw denim keffiyeh etsy art party before they sold out master cleanse gluten-free squid scenester freegan cosby sweater. Fanny pack portland seitan DIY, art party locavore wolf cliche high life echo park Austin. Cred vinyl keffiyeh DIY salvia PBR, banh mi before they sold out farm-to-table VHS viral locavore cosby sweater.</p>
  </div>
</div>

<script>
		//  ==================================== GIFFER (pause on .gif file) ====================================
window.onload = function() {
    Gifffer();
}

		//  ==================================== allow to filter on checkbox state ====================================
		/*
$(document).ready(function() {
     $("#filterRow input[type=checkbox]").click(function(){
     if($(this).is(":checked"))
     {
       $("input[type=checkbox]").each(function() {
       if($(this).is(":checked"))
        $(this).parent().parent().show();
       else
        $(this).parent().parent().hide();
       });

     }
     else
     {
       $("input[type=checkbox]").parent().parent().show();
     }
    });
});
*/



</script>

		<!--  ==================================== video js : alternative to the default video player ==================================== -->
  <script src="https://vjs.zencdn.net/7.8.1/video.js"></script>

  		<!--  ==================================== Basic autonomous little function to filter a table (test 1/3) ==================================== -->
  
<!-- function FilterTable() {
  var input, filter, table, tr, td, i, txtValue;
  input = document.getElementById("NameInputFilter");
  filter = input.value.toUpperCase();
  table = document.getElementById("PluginsTable");
  tr = table.getElementsByTagName("tr");
  for (i = 0; i < tr.length; i++) {
    td = tr[i].getElementsByTagName("td")[1];   // here you can choose which column to filter
    if (td) {
      txtValue = td.textContent || td.innerText;
      if (txtValue.toUpperCase().indexOf(filter) > -1) {
        tr[i].style.display = "";
      } else {
        tr[i].style.display = "none";
      }
    }       
  }
} -->

		<!--  ==================================== declaration for the library "jQuery.FilterTable" (test 2/3) ==================================== -->
  <!--  jQuery.FilterTable initialisation function  -->
   <!--
    <script>
    $(document).ready(function() {
        // apply filterTable to all tables on this page
        $('table').filterTable({filterExpression: 'filterTableFindAny'});
    });
    </script>

 -->

 		<!--  ==================================== declaration for the library "datatables" (test 3/3) ==================================== -->
<script>
$(document).ready(function() {
    // Setup - add a text input to each footer cell
    $('#PluginsTable tfoot th').each( function () {
        var title = $(this).text();
        $(this).html( '<input type="text" placeholder="Search '+title+'" />' );
    });
 
    // DataTable
    var table = $('#PluginsTable').DataTable({
        "initComplete": function () {
            // Apply the search
            this.api().columns().every( function () {
                var that = this;
				
				$('#PluginsTable tfoot tr').appendTo('#PluginsTable thead');   // To displays the search boxs at the top instead to the bottom of the table
                $( 'input', this.footer() ).on( 'keyup change clear', function () {
                    if ( that.search() !== this.value ) {
                        that
                            .search( this.value )
                            .draw();
                    }
                });
            });
        },
        "lengthMenu": [[5, 25, 50, -1], [5, 25, 50, "All"]]   //Page length options , cf. https://datatables.net/examples/advanced_init/length_menu
    });

});

  </script>
  
  </body></html>





<SCRIPT language="VBScript">
'*************************************************************************************
'
'
'	INI FILE CLASS AND SUB CLASSES START HERE 
'
'
'*************************************************************************************


'**********************************************************************************************************
' Programmer of INI FILE CLASS: Ludvik Jerabek   https://www.codeproject.com/Articles/21896/INI-Reader-Writer-Class-for-C-VB-NET-and-VBScript
'       Date: 02\14\2007
'    Purpose: Allow INI manipulation in VB Script using high efficiency hashing.
'             This code create a series of objects in memory which represent the INI file
'             structure. All objects are mapped into memory using the "Scripting.Dictionary"
'             object. The IniFile object is made up of IniSection objects, and IniSection objects
'             are made up of IniKey objects. See the sample code in this project to understand
'             usage. Expected efficiency should be O(log n) but depends on the efficiency
'             of the "Scripting.Dictionary" object.
'
'	Revisions
'
'	02\14\2007 - Ludvik Jerabek - Initial VBScript version
'	12\08\2007 - Ludvik Jerabek - Fixed regex parse string issue
'	08\23\2010 - Ludvik Jerabek - Rewrote version to uses hashing
'	11\12\2010 - Ludvik Jerabek - Fixed section regex matching on key values with brackets
'	06\20\2015 - Ludvik Jerabek - Fixed key parsing regex to account for keys with spaces in names
'
'**********************************************************************************************************

'IniFile class used to read and write ini files by loading the file into memory
Class IniFile
    'List of IniSection objects keeps track of all the sections in the INI file
    Private m_pSections
    'Public constructor
    Public Sub Class_Initialize()
        Set m_pSections = CreateObject("Scripting.Dictionary")
		m_pSections.CompareMode = vbTextCompare
    End Sub
    
    'Returns an array of the IniSections in the IniFile
    Public Property Get Sections
         Sections = m_pSections.Items
    End Property

    'Load IniFile object with existing INI Data
    Public Sub Load( ByVal sFileName , ByVal bAppend )
    	If Not bAppend Then RemoveAllSections() ' Clear the object...
    	
        Dim objFSO : Set objFSO = CreateObject("Scripting.FileSystemObject") 
        Dim tempsection : Set tempsection = Nothing
        Dim oReader : Set oReader = objFSO.OpenTextFile( sFileName )
        Dim regexsection : set regexsection = new regexp 
        Dim regexkey : Set regexkey = new regexp
		Dim regexcomment : Set regexcomment = new regexp
          
        regexcomment.Pattern = "^([\s]*#.*)"
		regexcomment.Global = False
		regexcomment.IgnoreCase = True
		regexcomment.MultiLine = False
		
		' Left for history
        	'regexsection.Pattern = "\[[\s]*([^\[\s].*[^\s\]])[\s]*\]"
        	regexsection.Pattern = "^[\s]*\[[\s]*([^\[\s].*[^\s\]])[\s]*\][\s]*$"
		regexsection.Global = False
		regexsection.IgnoreCase = True
		regexsection.MultiLine = False
		
		regexkey.Pattern = "^\s*([^=]*[^\s=])\s*=(.*)"
		regexkey.Global = False
		regexkey.IgnoreCase = True
		regexkey.MultiLine = False
		
        While Not oReader.AtEndOfStream
        	Dim line : line = oReader.ReadLine()
			If line <> "" Then
            	Dim m
                If regexcomment.Test(line) Then
                    Set m = regexcomment.Execute(line)
		    		'msgbox("Skipping Comment " & m.Item(0).subMatches.Item(0) )
                ElseIf regexsection.Test(line) Then
                    Set m = regexsection.Execute(line)
                    'msgbox("Adding section [" & m.Item(0).subMatches.Item(0) &"]" )
                    Set tempsection = AddSection( m.Item(0).subMatches.Item(0) )
                ElseIf regexkey.Test(line) And Not tempsection Is Nothing Then
                    Set m = regexkey.Execute(line)
                    'msgbox("Adding Key ["& m.Item(0).subMatches.Item(0) &"]=["& m.Item(0).subMatches.Item(1) &"]" )
                    tempsection.AddKey( m.Item(0).subMatches.Item(0) ).Value = m.Item(0).subMatches.Item(1)
				ElseIf Not tempsection Is Nothing Then
					'msgbox("Adding Key ["& line &"]" )
		    		tempsection.AddKey( line )
                'Else
                 '   msgbox("Skipping unknown type of data: " & line)
                End If
            End If
        Wend
        oReader.Close()
    End Sub


    'Allows you to do a save the IniFile resident in memory to file
    Public Sub Save(ByVal sFileName)
    	Dim objFSO : Set objFSO = CreateObject("Scripting.FileSystemObject")
        Dim oWriter : Set oWriter = objFSO.CreateTextFile( sFileName , True )
        Dim s 'IniSection
        Dim k 'IniKey
        For Each s In Sections
            'msgbox("Writing Section: " & s.Name)
            oWriter.WriteLine("[" & s.Name & "]")
            For Each k In s.Keys
				If k.Value <> vbNullString Then
	            	'msgbox("Writing Key: "&k.Name&"="&k.Value)
	                oWriter.WriteLine(k.Name & "="& k.Value )
				Else
					'msgbox("Writing Key: "&k.Name)
					oWriter.WriteLine(k.Name)
				End if
            Next
        Next
        oWriter.Close()
    End Sub

    'Returns the IniSection object associated with a section name
    Public Function GetSection(ByVal sSection )
        Set GetSection = Nothing
        sSection = Trim(sSection) 'Trim spaces
        If Len( sSection ) <> 0 Then
        	If m_pSections.Exists( sSection ) Then
        		Set GetSection = m_pSections.Item(sSection)
        	End If
        End If
    End Function

    ' Adds a section to the IniFile object, returns a IniSection object
    Public Function AddSection(ByVal sSection )
    	Set AddSection = Nothing
		If StrComp(TypeName(sSection),"IniSection",1) = 0 Then 
			If Not sSection Is Nothing Then
				' Only purpose is to be used by child to re-insert
				If Not sSection Is Nothing Then
					If Not m_pSections.Exists( sSection.Name ) Then
	       				Set m_pSections.Item( sSection.Name ) = sSection
	       				Set AddSection = sSection
	    	   		End If
				End If
			End If 
		ElseIf StrComp(TypeName(sSection),"String",1) = 0 Then
        	sSection = Trim(sSection)
        	If Len( sSection ) <> 0 Then 
	        	If m_pSections.Exists( sSection ) Then
        			Set AddSection = m_pSections.Item(sSection)
        		Else
	        		Dim s : Set s = New IniSection
        			Call s.Init( Me , sSection )
        			Set m_pSections.Item(sSection) = s
        			Set AddSection = s
        		End If
	        End If		
		End If
    End Function

    ' Removes all existing sections (clears the object) 
    Public Sub RemoveAllSections()
        Call m_pSections.RemoveAll()
    End Sub

	' Remove a section by name or section object
	Public Function RemoveSection(ByVal Obj)
		RemoveSection = False
		If StrComp(TypeName(Obj),"IniSection",1) = 0 Then 
			If Not Obj Is Nothing Then
				m_pSections.Remove(Obj.Name)
				RemoveSection = True
			End If 
		ElseIf StrComp(TypeName(Obj),"String",1) = 0 Then
			RemoveSection = RemoveSection( GetSection(Obj) )
		End If 
	End Function

	' Remove a key by section namd and key name
	Public Function RemoveKey(ByVal sSection , ByVal sKey)
		RemoveKey = False
        Dim s : Set s = GetSection(sSection)
        If Not s Is Nothing Then
            RemoveKey = s.RemoveKey( sKey )
        End If
	End Function
	
    ' Returns a KeyValue in a certain section
    Public Function GetKeyValue(ByVal sSection , ByVal sKey )
    	GetKeyValue = vbNullString
        Dim s : Set s = GetSection(sSection)
        If Not s Is Nothing Then
            Dim k : Set k = s.GetKey(sKey)
            If Not k Is Nothing Then
                GetKeyValue = k.Value
            End If
        End If
    End Function

    ' Sets a KeyValuePair in a certain section
    Public Function SetKeyValue(ByVal sSection , ByVal sKey , ByVal sValue )
        SetKeyValue = False
        Dim s : Set s = AddSection(sSection)
        If Not s Is Nothing Then
            Dim k : Set k = s.AddKey(sKey)
            If Not s Is Nothing Then
                k.Value = sValue
                SetKeyValue = True
            End If
        End If
    End Function

    ' Renames an existing section returns true on success, false if the section didn't exist or there was another section with the same sNewSection
    Public Function RenameSection(ByVal sSection , ByVal sNewSection)
        ' Note string trims are done in lower calls.
        RenameSection = False
        Dim s : Set s = GetSection(sSection)
        If Not s Is Nothing Then
            RenameSection = s.SetName(sNewSection)
        End If
    End Function

    ' Renames an existing key returns true on success, false if the key didn't exist or there was another section with the same sNewKey
    Public Function RenameKey(ByVal sSection , ByVal sKey, ByVal sNewKey)
        ' Note string trims are done in lower calls.
        RenameKey = False
        Dim s : Set s = GetSection(sSection)
        If Not s Is Nothing Then
            Dim k : Set k = s.GetKey(sKey)
            If Not k Is Nothing Then
                RenameKey = k.SetName(sNewKey)
            End If
        End If
    End Function
	
End Class


'IniSection Class 
Class IniSection
   ' IniFile IniFile object instance
   Private m_pIniFile
   ' Name of the section
   Private m_sSection
   ' List of IniKeys in the section
   Private m_keys

   'Friend constuctor so objects are internally managed
   Public Sub Class_Initialize
       Set m_pIniFile = Nothing
       m_sSection = ""
       Set m_keys = CreateObject("Scripting.Dictionary")
       m_keys.CompareMode = vbTextCompare
   End Sub

   	' Function only works once...
   	Public Sub Init( ByVal oIniFile , ByVal sSection )
   		If m_pIniFile is Nothing Then 
   			Set m_pIniFile = oIniFile
   			m_sSection = sSection
   		End If 	
   	End Sub

    'Returns an array of the IniKeys in the IniFile
    Public Property Get Keys
         Keys = m_keys.Items
    End Property
    
   'Returns the section name
   Public Property Get Name
           name = m_sSection
   End Property
    
   'Set the section name
   'Returns true on success, False if key already exists in the section
   Public Function SetName(ByVal sSection)
       SetName = False  ' Default
       sSection = Trim(sSection)
       If Len( sSection ) <> 0 Then
           Dim s : Set s = m_pIniFile.GetSection(sSection)
           If Not s Is Me And Not s Is Nothing Then Exit Function
           Call m_pIniFile.RemoveSection(Me)
           m_sSection = sSection
           Call m_pIniFile.AddSection(Me)           
           SetName = True
       End If
   End Function

   'Returns the section name
   Public Function GetName()
           GetName = m_sSection
   End Function
   
   'Adds a key to the IniSection object
   'Returns Nothing on failure
	Public Function AddKey(ByVal sKey)
		Set AddKey = Nothing
		' Is this a string or object of IniKey
		If StrComp(TypeName(sKey),"IniKey",1) = 0 Then 
			' Only purpose is to be used by child to re-insert
			If Not sKey Is Nothing Then
				If Not m_keys.Exists( sKey.Name ) Then
	       			Set m_keys.Item(sKey.Name) = sKey
	       			Set AddKey = sKey
	    	   	End If
			End If
		ElseIf StrComp(TypeName(sKey),"String",1) = 0 Then
			' String was passed...
			sKey = Trim(sKey)
			If Len(sKey) <> 0 Then
				If m_keys.Exists( sKey ) Then
		       		Set AddKey = m_keys.Item(sKey)
	    	   	Else
	       			Dim k : Set k = New IniKey
		       		Call k.Init( Me , sKey )
	       			Set m_keys.Item(sKey) = k
	       			Set AddKey = k
	       		End If
       		End If
		End If        
   End Function

   'Returns a IniKey
   'Returns Nothing on failure 
	Public Function GetKey(ByVal sKey)
   		Set GetKey = Nothing
    	sKey = Trim(sKey)
    	If Len(sKey) <> 0 Then
	       	If m_keys.Exists( sKey ) Then
	       		Set GetKey = m_keys.Item(sKey)
	       	End If 
		End If
	End Function

   'Removes all the keys in the section
   Public Sub RemoveAllKeys()
       Call m_keys.RemoveAll()
   End Sub
   
   'Removes a single key by IniKey object by string or object
   Public Function RemoveKey(ByVal Obj)
       	RemoveKey = False
		If StrComp(TypeName(Obj),"IniKey",1) = 0 Then 
			If Not Obj Is Nothing Then
			 	m_keys.Remove(Obj.Name)
				RemoveKey = True
			End If
		ElseIf StrComp(TypeName(Obj),"String",1) = 0 Then
			RemoveKey = RemoveKey( GetKey(Obj) )
		End If        
   End Function

End Class  ' End of IniSection
    
'IniKey Class
Class IniKey
    ' Name of the Key
    Private m_sKey
    ' Value associated
    Private m_sValue
    ' Pointer to the parent CIniSection
    Private m_pSection

    'Friend constuctor so objects are internally managed
    Public Sub Class_Initialize
    	m_sKey = ""
    	m_sValue = ""
        Set m_pSection = Nothing
    End Sub

    'Returns the key's parent IniSection
    Public Sub Init( ByVal oIniSection , ByVal sKey )
           If m_pSection Is Nothing Then
            	Set m_pSection = oIniSection
            	m_sKey = sKey
           End If 
    End Sub
    
    'Returns the name of the Key
    Public Property Get Name
            name = m_sKey
    End Property

'     'Gets\Sets the value associated with the Key
     Public Property Let Value( strKeyValue )
             m_sValue = strKeyValue
     End Property

    'Gets\Sets the value associated with the Key
    Public Property Get Value()
            value = m_sValue
    End Property

    'Sets the key name
    'Returns true on success, fails if the section name sKeyName already exists
    Public Function SetName(ByVal sKey )
    	SetName = False
        sKey = Trim(sKey)
        If Len(sKey) <> 0 Then
            Dim s : Set s = m_pSection.GetKey(sKey)
            If Not s Is Me And Not s Is Nothing Then Exit Function
            Call m_pSection.RemoveKey(Me)
            ' Set our new name
            m_sKey = sKey
            ' Put our own object back
            Call m_pSection.AddKey(Me)
            SetName = True
        End If
    End Function
    
    ' Returns the current key name
    Public Function GetName()
		    GetName = m_sKey
    End Function
    
End Class


'*************************************************************************************
'
'
'	INI FILE CLASS AND SUBCLASSES END HERE
'
'
'*************************************************************************************




</script>